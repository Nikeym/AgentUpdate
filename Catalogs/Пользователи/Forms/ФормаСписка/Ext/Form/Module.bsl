
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьОтображениеПользователей();
КонецПроцедуры

&НаКлиенте
Процедура ТЗПользователиИБВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Форма = ПолучитьФорму("Справочник.Пользователи.Форма.ФормаПользователяИБ");
	Форма.Имя = Элемент.ТекущиеДанные.Имя;
	Форма.Открыть();
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПользователей()
	ТЗПользователиИБ.Очистить();
	СписокПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Для Каждого Пользователь Из СписокПользователей Цикл
		НовСтр = ТЗПользователиИБ.Добавить();
		НовСтр.Имя = Пользователь.Имя;
		НовСтр.ПолноеИмя = Пользователь.ПолноеИмя;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗаписанПользователь" Тогда 
		ОбновитьОтображениеПользователей();
		СозданныйПользователь = ТЗПользователиИБ.НайтиСтроки(Новый Структура("Имя", Параметр));
		СозданныйПользовательКоличество = СозданныйПользователь.Количество();
		Если СозданныйПользовательКоличество > 0 Тогда
			Элементы.ТЗПользователиИБ.ТекущаяСтрока = СозданныйПользователь[СозданныйПользовательКоличество - 1].ПолучитьИдентификатор();
		КонецЕсли;                                                                                          
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаУдалить(РезультатВопроса, ПараметрыЗаписи) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьНаСервере(Элементы.ТЗПользователиИБ.ТекущиеДанные.Имя);
		ОбновитьОтображениеПользователей();
	КонецЕсли;	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНаСервере(сПользователь) 
	Пользователь = ПользователиИнформационнойБазы.НайтиПоИмени(сПользователь);
	Если НЕ Пользователь = Неопределено Тогда 
		Пользователь.Удалить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьОтображениеПользователей();
КонецПроцедуры

&НаКлиенте
Процедура ТЗПользователиИБПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Форма = ПолучитьФорму("Справочник.Пользователи.Форма.ФормаПользователяИБ");
	Форма.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура ТЗПользователиИБПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПоказатьВопрос(Новый ОписаниеОповещения("ОбработкаОтветаУдалить", ЭтотОбъект), "Вы точно хотите удалить пользователя " + Элементы.ТЗПользователиИБ.ТекущиеДанные.Имя + "?" , РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ТЗПользователиИБПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	Форма = ПолучитьФорму("Справочник.Пользователи.Форма.ФормаПользователяИБ");
	Форма.Имя = Элемент.ТекущиеДанные.Имя;
	Форма.Открыть();
КонецПроцедуры
