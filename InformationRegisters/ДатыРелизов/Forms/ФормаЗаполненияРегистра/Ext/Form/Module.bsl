
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()
	Для Каждого ТК Из Перечисления.ТестовыеКонтуры Цикл 
		НовСтр = ТестовыеКонтуры.Добавить();
		НовСтр.ТестовыйКонтур = ТК;
	КонецЦикла;
	ИнтервалРелизов = 7;
	ПоДату = Дата(Строка(формат(Год(ТекущаяДата())+2, "ЧГ=0")) + "0101000000");
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	Если ЗначениеЗаполнено(СДаты) И ЗначениеЗаполнено(ПоДату) И ТестовыеКонтуры.Количество() И ЗначениеЗаполнено(ИнтервалРелизов) Тогда
		ЗаполнитьНаСервере();
	Иначе 
		ПоказатьПредупреждение(,"Заполните все поля!");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Набор = РегистрыСведений.ДатыРелизов.СоздатьНаборЗаписей();
	Выборка = РегистрыСведений.ДатыРелизов.Выбрать(,СДаты-1);
	Пока Выборка.Следующий() Цикл 
		НовЗап = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(НовЗап, Выборка);
	КонецЦикла;
	
	СчетчикДата = СДаты;
	КоличествоТК = ТестовыеКонтуры.Количество();
	ПозицияЗаписи = 0;
	Пока СчетчикДата <= ПоДату Цикл
		НовЗап = Набор.Добавить();
		НовЗап.Период = СчетчикДата;
		НовЗап.ТестовыйКонтур = ТестовыеКонтуры[ПозицияЗаписи].ТестовыйКонтур;
		СчетчикДата = СчетчикДата + 60*60*24*ИнтервалРелизов;
		ПозицияЗаписи = ?(ПозицияЗаписи+1>КоличествоТК-1, 0, ПозицияЗаписи+1);
	КонецЦикла;
	
	Набор.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРелизыКТК()
	Если ЗначениеЗаполнено(ИнтервалРелизов) И ТестовыеКонтуры.Количество() Тогда
		Если ЗначениеЗаполнено(СДаты) Тогда 
			СчетчикДаты = СДаты;
			Для Каждого Запись Из ТестовыеКонтуры Цикл 
				Запись.Релиз = СчетчикДаты;
				СчетчикДаты = СчетчикДаты + 60*60*24*ИнтервалРелизов;
			КонецЦикла;
		Иначе 
			Для Каждого Запись Из ТестовыеКонтуры Цикл 
				Запись.Релиз = СДаты;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СДатыПриИзменении(Элемент)
	Если СДаты < ТекущаяДата() Тогда
		СДаты = Дата("00010101000000");
		ПоказатьПредупреждение(,"Нельзя вносить изменения в предыдущие периоды! Значение ""Заполнить с даты"" должно быть больше текущей даты.");
		ЭтаФорма.ТекущийЭлемент = Элементы["СДаты"];
	КонецЕсли;
	ЗаполнитьРелизыКТК();
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалРелизовПриИзменении(Элемент)
	ЗаполнитьРелизыКТК();
КонецПроцедуры

&НаКлиенте
Процедура ТестовыеКонтурыПриИзменении(Элемент)
	ЗаполнитьРелизыКТК();
КонецПроцедуры